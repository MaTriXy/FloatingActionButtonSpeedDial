/*
 * Copyright 2022 Roberto Leinardi.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'speeddial.android-library-conventions'
    id 'speeddial.artifact-publish-library-conventions'
    alias(libs.plugins.errorprone)
    //    alias(libs.plugins.checkstyle) // TODO
}

ext {
    PUBLISH_GROUP_ID = 'com.leinardi.android'
    PUBLISH_VERSION = config.android.version_name_view
    PUBLISH_ARTIFACT_ID = 'speed-dial'
}

android {
    defaultConfig {
        versionName config.android.version_name_view
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
}

tasks.withType(JavaCompile) {
    // remove the if condition if you want to run NullAway on test code
    if (!name.toLowerCase().contains("test")) {
        options.errorprone {
            check("NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "com.leinardi.android.speeddial")
        }
    }
}

dependencies {
    api libs.androidx.appcompat
    api libs.androidx.cardview
    api libs.material
    annotationProcessor libs.nullaway
    errorprone libs.errorprone.core
    errorproneJavac libs.errorprone.javac
}
